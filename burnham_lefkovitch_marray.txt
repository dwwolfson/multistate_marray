
model {

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
# 1. SPECIFY PRIORS
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

# pi is proportion of AHY that are subadults at time of banding
pi.mu ~ dnorm(0,0.33)
pi.sd ~ dunif(0.01, 2); pi.tau <- pow(pi.sd, -2)
# g is proportion of surviving subadults that transition to become adult breeders
g.mu ~ dnorm(0,0.33)
g.sd ~ dunif(0.01, 2); g.tau <- pow(g.sd, -2)
for (t in 1:(n_yrs)){
  logit.pi[t] ~ dnorm(pi.mu, pi.tau)
  logit(pi[t]) <- logit.pi[t]
  logit.g[t] ~ dnorm(g.mu, g.tau)
  logit(g[t]) <- logit.g[t]
}

# age-specific hyperparameters 
# include code for temporal covariation across age classes?
for (a in 1:n_ages){
  s.mu[a] ~ dnorm(0, 0.33) # survival
  s.sd[a] ~ dunif(0.01, 2); s.tau[a] <- pow(s.sd[a], -2) # annual variation, convert to precision
  F.mu[a] ~ dnorm(0, 0.33) # fidelity to breeding site
  F.sd[a] ~ dunif(0.01, 2); F.tau[a] <- pow(F.sd[a], -2)
  r.mu[a] ~ dnorm(0, 0.33) # seber recovery probability
  r.sd[a] ~ dunif(0.01, 2); r.tau[a] <- pow(r.sd[a], -2)
  p.mu[a] ~ dnorm(0, 0.33)  # recapture probability, same site
  p.sd[a] ~ dunif(0.01, 2); p.tau[a] <- pow(p.sd[a], -2)
  for (t in 1:(n_yrs)){
    logit.s[a,t] ~ dnorm(s.mu[a], s.tau[a])
    logit(s[a,t]) <- logit.s[a,t] # backtransform to real scale 
    logit.F[a,t] ~ dnorm(F.mu[a], F.tau[a])
    logit(F[a,t]) <- logit.F[a,t]  
    logit.r[a,t] ~ dnorm(r.mu[a], r.tau[a])
    logit(r[a,t]) <- logit.r[a,t]  
    logit.p[a,t] ~ dnorm(p.mu[a], p.tau[a])
    logit(p[a,t]) <- logit.p[a,t]  
  } # t
} # a

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
# 2. Multistate state-transition and observation matrix (a vector suffices)
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

# alive resident 1: HY, 2: SY, 3: ASY, 4: AHY
# recovered dead, all ages: 5 (thanks Thomas!)
# alive emigrant 6: SY, 7: ASY (unobservable states)
# dead or unobserved (absorbing)

for (t in 1:(n_yrs-1)){
  # departure HY release
   psi[1,t,1] <- 0
   psi[1,t,2] <- s[1,t]*F[1,t] # survive to subadult, remain
   psi[1,t,3] <- 0
   psi[1,t,4] <- 0
   psi[1,t,5] <- (1-s[1,t])*r[1,t] # die, recovered
   psi[1,t,6] <- s[1,t]*(1-F[1,t]) # survive, emigrate
   psi[1,t,7] <- 0
   psi[1,t,8] <- (1-s[1,t])*(1-r[1,t]) # die, not recovered
  # departure subadult resident
   psi[2,t,1] <- 0
   psi[2,t,2] <- s[2,t]*F[2,t]*(1-g[t]) # persist as resident subadults
   psi[2,t,3] <- s[2,t]*F[2,t]*g[t] # graduate to resident adults
   psi[2,t,4] <- 0
   psi[2,t,5] <- (1-s[2,t])*r[2,t]
   psi[2,t,6] <- s[2,t]*(1-F[2,t])*(1-g[t]) # persist as dispersed subadults
   psi[2,t,7] <- s[2,t]*(1-F[2,t])*g[t] # graduate to dispersed adults
   psi[2,t,8] <- (1-s[2,t])*(1-r[2,t])
  # departure adult resident
   psi[3,t,1] <- 0
   psi[3,t,2] <- 0
   psi[3,t,3] <- s[3,t]*F[3,t]
   psi[3,t,4] <- 0
   psi[3,t,5] <- (1-s[3,t])*r[3,t]
   psi[3,t,6] <- 0
   psi[3,t,7] <- s[3,t]*(1-F[3,t])
   psi[3,t,8] <- (1-s[3,t])*(1-r[3,t])
  # departure AHY resident (individuals starting as subadult can persist or graduate)
   psi[4,t,1] <- 0
   psi[4,t,2] <- pi[t]*s[2,t]*F[2,t]*(1-g[t]) # persist as subadult
   psi[4,t,3] <- pi[t]*s[2,t]*F[2,t]*g[t] + (1-pi[t])*s[3,t]*F[3,t] # graduate or start as adult
   psi[4,t,4] <- 0
   psi[4,t,5] <- pi[t]*(1-s[2,t])*r[2,t] + (1-pi[t])*(1-s[3,t])*r[3,t]
   psi[4,t,6] <- pi[t]*s[2,t]*(1-F[2,t])*(1-g[t])
   psi[4,t,7] <- pi[t]*s[2,t]*(1-F[2,t])*g[t] + (1-pi[t])*s[3,t]*(1-F[3,t])
   psi[4,t,8] <- pi[t]*(1-s[2,t])*(1-r[2,t]) + (1-pi[t])*(1-s[3,t])*(1-r[3,t])
  # recently dead (simplified coding, all transition to long dead) 
   psi[5,t,1:8] <- c(0, 0, 0, 0, 0, 0, 0, 1)
  # departure subadult emigrant
   psi[6,t,1] <- 0
   psi[6,t,2] <- 0
   psi[6,t,3] <- 0
   psi[6,t,4] <- 0
   psi[6,t,5] <- (1-s[2,t])*r[2,t]
   psi[6,t,6] <- s[2,t]*(1-g[t])
   psi[6,t,7] <- s[2,t]*g[t]
   psi[6,t,8] <- (1-s[2,t])*(1-r[2,t])
  # departure adult emigrant
   psi[7,t,1] <- 0
   psi[7,t,2] <- 0
   psi[7,t,3] <- 0
   psi[7,t,4] <- 0
   psi[7,t,5] <- (1-s[3,t])*r[3,t]
   psi[7,t,6] <- 0
   psi[7,t,7] <- s[3,t]
   psi[7,t,8] <- (1-s[3,t])*(1-r[3,t])
  # already dead (simplified coding) 
   psi[8,t,1:8] <- c(0, 0, 0, 0, 0, 0, 0, 1)

 # observation matrix 
   po[1,t] <- 1 # HY releases, all seen (but condition on capture so ignored)
   po[2,t] <- p[2,t] # captured as SY where last captured
   po[3,t] <- p[3,t] # captured as ASY where last captured
   po[4,t] <- 1 # AHY releases
   po[5,t] <- 1 # dead recoveries, r in state-transition matrix
   po[6,t] <- 0 # SY emigrants, unobservable
   po[7,t] <- 0 # ASY emigrant, unobservable
   po[8,t] <- 0 # dead unrecovered or long dead

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
# Miche's magic code, never needs to be modified
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

 # Calculate probability of non-encounter (dq) and reshape the array for the encounter probabilities
      for (k in 1:ns){
         dp[k,t,k] <- po[k,t]
         dq[k,t,k] <- 1-po[k,t]} #k
      for (k in 1:(ns-1)){
         for (m in (k+1):ns){
            dp[k,t,m] <- 0
            dq[k,t,m] <- 0} #m
      } #k
      for (k in 2:ns){
         for (m in 1:(k-1)){
            dp[k,t,m] <- 0
            dq[k,t,m] <- 0} #m
      } #k
   } #t

# Define the multinomial likelihood
for (t in 1:((n_yrs-1)*ns)){
   marr[t,1:(n_yrs*ns-(ns-1))] ~ dmulti(pri[t,], rel[t])
} # t

# Define the cell probabilities of the multistate m-array
for (t in 1:(n_yrs-2)){
   U[(t-1)*ns+(1:ns), (t-1)*ns+(1:ns)] <- ones
   for (j in (t+1):(n_yrs-1)){
   ## fixed based on published erratum of book code
      U[(t-1)*ns+(1:ns), (j-1)*ns+(1:ns)] <- U[(t-1)*ns+(1:ns), (j-2)*ns+(1:ns)] %*% psi[,j-1,] %*% dq[,j-1,] # fixed
   } #j
} #t
U[(n_yrs-2)*ns+(1:ns), (n_yrs-2)*ns+(1:ns)] <- ones

# Diagonal
for (t in 1:(n_yrs-2)){
   pri[(t-1)*ns+(1:ns),(t-1)*ns+(1:ns)] <- U[(t-1)*ns+(1:ns),(t-1)*ns+(1:ns)] %*% psi[,t,] %*% dp[,t,]
   # Above main diagonal
   for (j in (t+1):(n_yrs-1)){
      pri[(t-1)*ns+(1:ns), (j-1)*ns+(1:ns)] <- U[(t-1)*ns+(1:ns), (j-1)*ns+(1:ns)] %*% psi[,j,] %*% dp[,j,]
   } #j
} #t
pri[(n_yrs-2)*ns+(1:ns), (n_yrs-2)*ns+(1:ns)] <- psi[,n_yrs-1,] %*% dp[,n_yrs-1,]

# Below main diagonal
for (t in 2:(n_yrs-1)){
   for (j in 1:(t-1)){
      pri[(t-1)*ns+(1:ns),(j-1)*ns+(1:ns)] <- zero
   } #j
} #t

# Last column: probability of non-recapture
for (t in 1:((n_yrs-1)*ns)){
   pri[t,(n_yrs*ns-(ns-1))] <- 1-sum(pri[t,1:((n_yrs-1)*ns)])
} #t
} # end model
